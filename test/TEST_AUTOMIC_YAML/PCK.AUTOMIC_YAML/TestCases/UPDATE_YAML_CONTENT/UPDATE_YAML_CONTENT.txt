*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        UPDATE_YAMLContent
Test Template     UPDATE_YAMLContent Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    YAMLFilePath         YAMLContent        YAMLPath          Is_Array    Value       DownloadPath
UPDATE_YAML_Content from YAMLFile
                      [Tags]    main_scenarios
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      NO          ${Value}    ${Download_path}

UPDATE_YAML_ARRAY in YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Array}     YES         ${Value}    ${Download_path1}

UPDATE_YAML_OBJECT in YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Object}    NO          ${Value}    ${Download_path2}

UPDATE_YAML from YAMLContent
                      OK        ${EMPTY}             ${YAML_Content}    ${YAML_Path1}     NO          ${Value}    ${Download_path3}

UPDATE_YAMLContent with invalid YAML_File_Path
                      NOK       Invalid              ${EMPTY}           ${YAML_Path}      NO          ${Value}    ${Download_path}

UPDATE_YAMLContent with invalid YAMLPath
                      NOK       ${YAML_File_Path}    ${EMPTY}           Invalid           NO          ${Value}    ${Download_path}

UPDATE_YAMLContent with invalid Is_Array
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      Invalid     ${Value}    ${Download_path}

UPDATE_YAMLContent with Invalid Download Path
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      NO          ${Value}    Invalid

UPDATE_YAMLContent with both yamlFile and yamlContent
                      NOK       ${YAML_File_Path}    ${YAML_Content}    ${YAML_Path}      NO          ${Value}    ${Download_path}

UPDATE_YAMLContent with both YAMLFile and YAMLContent as empty
                      NOK       ${EMPTY}             ${EMPTY}           ${YAML_Path}      NO          ${Value}    ${Download_path}

UPDATE_YAMLContent with Empty Value
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      NO          ${EMPTY}    ${Download_path}

UPDATE_YAMLContent with Empty Download Path
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      NO          ${Value}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "UPDATE_YAML_CONTENT"*******
    Log    *******Connect to AE*******
    Connect AE

UPDATE_YAML_CONTENT Template
    [Arguments]    ${OK_NOK}    ${YAMLFilePath}    ${YAMLContent}    ${YAMLPath}    ${Is_Array}    ${Value}
    ...    ${DownloadPath}
    [Documentation]    This test tests the feature of UPDATE_YAML_CONTENT.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this Name should be OK or NOK.
    ...    -${YAMLFilePath}: This field specifies the full path to the YAML file.
    ...    -${YAMLContent}: This field specifies the YAML content or a variable that contains YAML content.
    ...    -${YAMLPath}: This field specifies path to the element in YAML content to be updated.
    ...    -${Is_Array}: This field specifies if element being updates is an array type.
    ...    -${Value}: This field specifies the new value to be set.
    ...    -${DownloadPath}: This field Specify path to download modified file. E.g. D:\YAML\filename.YAML
    Generate String
    Init Action    ${UPDATE_YAML_CONTENT_ACTION}
    Run Keyword If    '${YAMLFilePath}' == 'Invalid'    Action Set    &UC4RB_YAML_FILE_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_FILE_PATH#    ${YAMLFilePath}
    Run Keyword If    '${YAMLContent}' == 'Invalid'    Action Set    &UC4RB_YAML_CONTENT#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_CONTENT#    ${YAMLContent}
    Run Keyword If    '${YAMLPath}' == 'Invalid'    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${YAMLPath}
    Run Keyword If    '${Is_Array}' == 'Invalid'    Action Set    &UC4RB_YAML_IS_ARRAY#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_IS_ARRAY#    ${Is_Array}
    Run Keyword If    '${Value}' == 'Invalid'    Action Set    &UC4RB_YAML_VALUE#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_VALUE#    ${Value}
    Run Keyword If    '${DownloadPath}' == 'Invalid'    Action Set    &UC4RB_YAML_DOWNLOAD_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_DOWNLOAD_PATH#    ${DownloadPath}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    ...    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_YAML_UPDATED_YAML#
    ...    ELSE    Action Object Variable Should Not Be Found    &UC4RB_YAML_UPDATED_YAML#

Teardown
    Log    *******End Test "UPDATE_YAML_CONTENT"*******
