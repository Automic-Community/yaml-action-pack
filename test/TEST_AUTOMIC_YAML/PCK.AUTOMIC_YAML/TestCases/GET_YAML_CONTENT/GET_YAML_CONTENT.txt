*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        GET_YAML_CONTENT
Test Template     GET_YAML_CONTENT Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    YAMLFilePath         YAMLContent        YAMLPath          Fail_If_Path_Not_Exist
Get_YAML_Content from YAMLFile
                      [Tags]    main_scenarios
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Path}      YES

Get_YAML_Array from YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Array}     YES

Get_YAML_Object from YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Object}    YES

Get_YAML_Content from YAMLContent
                      OK        ${EMPTY}             ${YAML_Content}    ${YAML_Path1}     YES

Get_YAML_Content from both YAML_File_Path and YAMLContent
                      NOK       ${YAML_File_Path}    ${YAML_Content}    ${YAML_Path1}     YES

Get_YAML_Content from invalid YAML_File_Path
                      NOK       Invalid              ${EMPTY}           ${YAML_Path}      YES

Get_YAML_Content from non-existing YAMLPath and FailPath as NO
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAML_Path1}     NO

Get_YAML_Content having invalid Path
                      NOK       ${YAML_File_Path}    ${EMPTY}           Invalid           YES

Get_YAML_Content when both YAML_File_Path and YAMLContent are empty
                      NOK       ${EMPTY}             ${EMPTY}           ${YAML_Path}      YES

Get_YAML_Content having Empty Path
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${EMPTY}          YES

*** Keywords ***
Startup
    Log    *******Start Test "Get_YAML_Content"*******
    Log    *******Connect to AE*******
    Connect AE

GET_YAML_CONTENT Template
    [Arguments]    ${OK_NOK}    ${YAMLFilePath}    ${YAMLContent}    ${YAMLPath}    ${Fail_If_Path_Not_Exist}
    [Documentation]    This test tests the feature of Get YAMLContent.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${YAMLFilePath}: This field specifies the full path to YAML file. E.g.: C:/Files/response.YAML.
    ...    -${YAMLContent}: This field specifies YAML content or a variable that contains YAML content.
    ...    -${YAMLPath}: This field specifies valid path to the YAML element to retrieve its value. E.g.: $.Dev.Server
    ...    -${Fail_If_Path_Not_Exist}: This field specifies whether the action should fail if the path does not exist.
    Generate String
    Init Action    ${GET_YAML_CONTENT_ACTION}
    Run Keyword If    '${YAMLFilePath}' == 'Invalid'    Action Set    &UC4RB_YAML_FILE_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_FILE_PATH#    ${YAMLFilePath}
    Run Keyword If    '${YAMLContent}' == 'Invalid'    Action Set    &UC4RB_YAML_CONTENT#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_CONTENT#    ${YAMLContent}
    Run Keyword If    '${YAMLPath}' == 'Invalid'    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${YAMLPath}
    Run Keyword If    '${Fail_If_Path_Not_Exist}'    Action Set    &UC4RB_YAML_FAIL#    ${Fail_If_Path_Not_Exist}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    ...    ELSE    Assert Failure

Teardown
    Log    *******End Test "Get_YAML_Content"*******
