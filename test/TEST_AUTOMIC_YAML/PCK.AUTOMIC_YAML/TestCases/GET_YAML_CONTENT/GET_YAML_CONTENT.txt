*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        GET_YAML_CONTENT
Test Template     GET_YAML_CONTENT Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    YML_File_Path      YML_Content       YML_Path         Fail_If_Path_Not_Exist
Get_YAML_Content from YAMLFile
                      [Tags]    main_scenarios
                      OK        ${YAMLFilePath}    ${EMPTY}          ${YAMLPath}      YES

Get_YAML_Array from YAMLFile
                      OK        ${YAMLFilePath}    ${EMPTY}          ${YAMLArray}     YES

Get_YAML_Object from YAMLFile
                      OK        ${YAMLFilePath}    ${EMPTY}          ${YAMLObject}    YES

Get_YAML_Content from YAMLContent
                      OK        ${EMPTY}           ${YAMLContent}    ${YAMLPath1}     YES

Get_YAML_Content from both YAMLFilePath and YAMLContent
                      NOK       ${YAMLFilePath}    ${YAMLContent}    ${YAMLPath2}     YES

Get_YAML_Content from invalid YAMLFilePath
                      NOK       Invalid            ${EMPTY}          ${YAMLPath}      YES

Get_YAML_Content from non-existing YAMLPath and FailPath as NO
                      OK        ${YAMLFilePath}    ${EMPTY}          ${YAMLPath1}     NO

Get_YAML_Content having invalid Path
                      NOK       ${YAMLFilePath}    ${EMPTY}          Invalid          YES

Get_YAML_Content from invalid YAMLContent
                      NOK       ${EMPTY}           Invalid           ${YAMLPath}      YES

Get_YAML_Content when both YAMLFilePath and YAMLContent are empty
                      NOK       ${EMPTY}           ${EMPTY}          ${YAMLPath}      YES

Get_YAML_Content having Empty Path
                      NOK       ${YAMLFilePath}    ${EMPTY}          ${EMPTY}         YES

*** Keywords ***
Startup
    Log    *******Start Test "Get_YAML_Content"*******
    Log    *******Connect to AE*******
    Connect AE

GET_YAML_CONTENT Template
    [Arguments]    ${OK_NOK}    ${YML_File_Path}    ${YML_Content}    ${YML_Path}    ${Fail_If_Path_Not_Exist}
    [Documentation]    This test tests the feature of Get YAMLContent.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${YML_File_Path}: This field specifies the full path to YAML file. E.g.: C:/Files/response.YAML.
    ...    -${YML_Content}: This field specifies YAML content or a variable that contains YAML content.
    ...    -${YML_Path}: This field specifies valid path to the YAML element to retrieve its value. E.g.: $.Dev.Server
    ...    -${Fail_If_Path_Not_Exist}: This field specifies whether the action should fail if the path does not exist.
    Generate String
    Init Action    ${GET_YAML_CONTENT_ACTION}
    Run Keyword If    '${YML_File_Path}' == 'Invalid'    Action Set    &UC4RB_YAML_FILE_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_FILE_PATH#    ${YML_File_Path}
    Run Keyword If    '${YML_Content}' == 'Invalid'    Action Set    &UC4RB_YAML_CONTENT#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_CONTENT#    ${YML_Content}
    Run Keyword If    '${YML_Path}' == 'Invalid'    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_ELEMENT_PATH#    ${YML_Path}
    Run Keyword If    '${Fail_If_Path_Not_Exist}'    Action Set    &UC4RB_YAML_FAIL#    ${Fail_If_Path_Not_Exist}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    ...    ELSE    Assert Failure

Teardown
    Log    *******End Test "Get_YAML_Content"*******
