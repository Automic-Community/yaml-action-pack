*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        INSERT_YAML_CONTENT
Test Template     INSERT_YAML_CONTENT Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/Names.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    YAMLFilePath         YAMLContent        YAML_Position                 Key             Value             DownloadPath
INSERT_YAML_CONTENT in YAMLFile
                      [Tags]    main_scenarios
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${Key}          ${Value}          ${Download_path}

INSERT_YAML_ARRAY in YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${ArrayKey}     ${ArrayValue}     ${Download_path1}

INSERT_YAML_OBJECT in YAMLFile
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${ObjectKey}    ${ObjectValue}    ${Download_path2}

INSERT_YAML in YAMLContent
                      [Tags]    main_scenarios
                      OK        ${EMPTY}             ${YAML_Content}    ${InsertInYAMLContentPath}    ${Key}          ${Value}          ${Download_path3}

INSERT_YAML_CONTENT when invalid YAML_File_Path
                      NOK       Invalid              ${EMPTY}           ${YAMLPosition}               ${Key}          ${Value}          ${Download_path}

INSERT_YAML_CONTENT with invalid YAMLPosition
                      NOK       ${YAML_File_Path}    ${EMPTY}           Invalid                       ${Key}          ${Value}          ${Download_path}

INSERT_YAML_CONTENT with Invalid Download Path
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${Key}          ${Value}          Invalid

INSERT_YAML_CONTENT with both yamlFile and yamlContent
                      NOK       ${YAML_File_Path}    ${YAML_Content}    ${YAMLPosition}               ${Key}          ${Value}          ${Download_path}

INSERT_YAML_CONTENT with both YAMLFile and YAMLContent as empty
                      NOK       ${EMPTY}             ${EMPTY}           ${YAMLPosition}               ${Key}          ${Value}          ${Download_path}

INSERT_YAML_CONTENT with EMPTY YAMLPosition
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${EMPTY}                      ${Key}          ${Value}          ${Download_path}

INSERT_YAML_CONTENT with Empty Key
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${EMPTY}        ${Value}          ${Download_path}

INSERT_YAML_CONTENT with Empty Value
                      NOK       ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${Key}          ${EMPTY}          ${Download_path}

INSERT_YAML_CONTENT with Empty Download Path
                      OK        ${YAML_File_Path}    ${EMPTY}           ${YAMLPosition}               ${Key}          ${Value}          ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "INSERT_YAML_CONTENT"*******
    Log    *******Connect to AE*******
    Connect AE

INSERT_YAML_CONTENT Template
    [Arguments]    ${OK_NOK}    ${YAMLFilePath}    ${YAMLContent}    ${YAML_Position}    ${Key}    ${Value}
    ...    ${DownloadPath}
    [Documentation]    This test tests the feature of INSERT_YAML_CONTENT.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this Name should be OK or NOK.
    ...    -${YAMLFilePath}: This field specifies the full path to the YAML file.
    ...    -${YAMLContent}: This field specifies YAML content or a variable that contains YAML content.
    ...    -${YAML_Position}: This field specifies path to the target location in the YAML to add new element.
    ...    -${Key}: This field specifies the key to the YAML element to be added.
    ...    -${Value}: This field specifies the value to be added to the specified location.
    ...    -${DownloadPath}: This field Specify a path to download the modified file. E.g. D:\YAML\filename.YAML
    Generate String
    Init Action    ${INSERT_YAML_CONTENT_ACTION}
    Run Keyword If    '${YAMLFilePath}' == 'Invalid'    Action Set    &UC4RB_YAML_FILE_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_FILE_PATH#    ${YAMLFilePath}
    Run Keyword If    '${YAMLContent}' == 'Invalid'    Action Set    &UC4RB_YAML_CONTENT#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_CONTENT#    ${YAMLContent}
    Run Keyword If    '${YAML_Position}' == 'Invalid'    Action Set    &UC4RB_YAML_POSITION#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_POSITION#    ${YAML_Position}
    Run Keyword If    '${Key}' == 'Invalid'    Action Set    &UC4RB_YAML_KEY#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_KEY#    ${Key}
    Run Keyword If    '${Value}' == 'Invalid'    Action Set    &UC4RB_YAML_VALUE#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_VALUE#    ${Value}
    Run Keyword If    '${DownloadPath}' == 'Invalid'    Action Set    &UC4RB_YAML_DOWNLOAD_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_YAML_DOWNLOAD_PATH#    ${DownloadPath}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    ...    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_YAML_DATA#
    ...    ELSE    Action Object Variable Should Not Be Found    &UC4RB_YAML_DATA#

Teardown
    Log    *******End Test "INSERT_YAML_CONTENT"*******
